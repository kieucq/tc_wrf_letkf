!
! [NOTE]:      This program is to preprocess the data in the format generated by 
!              a bogus vortex program so that the obs data is re-mapped to the 
!              C-grid defined by wrfinput_d01 (WRF-ARW)
!
! [INPUT]:     Two inputs are required: a wrfinput_d01 generated by real.exe
!              and an input text file containing the vortex profiles created
!              by bogus vortex program
!
! [OUTPUT]:    a text file containing profile of bogus obs mapped on the grid
!              point of the C-grid associated with the wrfinput_d01
!
! [HISTORY]: - 05 Nov 2012: craeted from the bogus vortex program
!
! [AUTHOR]:    Chanh Q. Kieu, NOAA scientist
!              Email: chanh.kieu@noaa.gov
!
! [REFERENCE]: Kieu, C. Q. (2005): arxiv.org
!
! [COPYRIGHT]: (C) 2012
!
!===========================================================================
!
      PROGRAM preprocess_obs_to_letkf
      IMPLICIT none
      INCLUDE 'netcdf.inc' 
!
! netcdf attribution io
!
      INTEGER, PARAMETER :: n=4
      INTEGER            :: ncid,status
      INTEGER            :: ndims,nvars,ngatts,unlimdimid
      INTEGER            :: uid,vid,wid,tid,pid
      INTEGER            :: undim,vndim,wndim,tndim
      INTEGER            :: udim(n),vdim(n),wdim(n),tdim(n)
!
! C-grid info from the wrfinput_D01
!
      INTEGER            :: nx,ny,nz,nt,nx1,ny1,nz1,nt1             ! wrf grid dim
      REAL               :: dx,dy,dz                                ! wrf grid size
      REAL,ALLOCATABLE   :: wrf_z(:,:,:,:),wrf_zp(:,:,:,:)          ! wrf level z on grid
      REAL,ALLOCATABLE   :: wrf_p(:,:,:,:),wrf_pp(:,:,:,:)          ! wrf level p on grid
      REAL,ALLOCATABLE   :: wrf_lat(:,:,:),wrf_lon(:,:,:)           ! wrf lat/lon grid 
      REAL,ALLOCATABLE   :: wrf_T(:,:,:,:)                          ! wrf poten temp perturb
      REAL,ALLOCATABLE   :: znu(:),znw(:)                           ! wrf eta levels
!
! observation info
!
      INTEGER            :: nox,noy,noz,no                          ! obs dimensions
      REAL,ALLOCATABLE   :: u_o(:),v_o(:),t_o(:),z_o(:),p_o(:)      ! obs variables
      REAL,ALLOCATABLE   :: q_o(:),zp_o(:)
      REAL,ALLOCATABLE   :: lat_o(:),lon_o(:),lev_o(:)              ! obs locations
      CHARACTER          :: vertical_type                           ! obs vertical coor type
      REAL               :: u_err,v_err,t_err,z_err,q_err           ! obs errors
!
! constant and local indexes
!
      REAL               :: pi,G,cp,rd,roref,pref,tref
      INTEGER            :: ic,jc,nmax
      REAL               :: rtime,dt,tfcst,restart,mis,r_obs
      INTEGER            :: obs_flag,ne,itime,ntime
      CHARACTER*100      :: name,tem
      INTEGER            :: i,j,k,irec,tem_len,debug,ncount
      REAL               :: i_o,j_o,k_o,w1,w2
      REAL,ALLOCATABLE   :: tem4d(:,:,:,:)
!
! initialized some parameters
!
      irec               = 1
      debug              = 1
      pi                 = 4.*atan(1.)
      G                  = 9.81
      roref              = 1.
      pref               = 1.01e+05
      tref               = 300.
      cp                 = 1004.
      rd                 = 287.
      mis                = -99999
!
! open the input file generated by the real.exe first
!
      status = NF_OPEN('wrfinput_d01',nf_write,ncid)
      PRINT*,''
      PRINT*,'prep_obs.exe: 1. NF_OPEN and return value: '
      PRINT*,'prep_obs.exe: status     =',status
      PRINT*,'prep_obs.exe: nf_noerr   =',nf_noerr 
      PRINT*,'prep_obs.exe: ncid       =',ncid
!
! probing some dimension and structure of data fields
!
      status = NF_INQ(ncid, ndims, nvars, ngatts, unlimdimid)
      PRINT*,''
      PRINT*,'prep_obs.exe: 2. NF_INQ returns values: '
      PRINT*,'prep_obs.exe: ncid       =',ncid            ! file id
      PRINT*,'prep_obs.exe: ndims      =',ndims           ! number of dims
      PRINT*,'prep_obs.exe: nvars      =',nvars           ! number of varibales
      PRINT*,'prep_obs.exe: ngatts     =',ngatts          ! number of globale attributes
      PRINT*,'prep_obs.exe: unlimdimid =',unlimdimid      ! number of umlimitted dim
!
! pull our varid and dim information for a temporary variable
! which will be used later.  
!
      tem                = 'T'
      tem_len            = len_trim(tem)
      PRINT*,'prep_obs.exe: Checking var is: ',tem(1:tem_len)
      status = NF_INQ_VARID(ncid,tem(1:tem_len),uid)
      status = NF_INQ_VARNDIMS(ncid,uid,undim)
      status = NF_INQ_VARDIMID(ncid,uid,udim)
      IF (undim.ne.n) THEN
       PRINT*,'prep_obs.exe: Number of dim is /= 4...re-allocation now'
       status            = NF_CLOSE(ncid)
       STOP
      ENDIF
      PRINT*,''
      PRINT*,'prep_obs.exe: 3. Pull out information about: ',tem(1:tem_len)
      PRINT*,'prep_obs.exe: uid        =',uid
      PRINT*,'prep_obs.exe: undim      =',undim
      DO i               = 1,undim
       j                 = udim(i)
       status            = NF_INQ_DIMNAME(ncid,j,name)
       status            = NF_INQ_DIMLEN(ncid,j,k)
       IF (i.eq.1) nx    = k
       IF (i.eq.2) ny    = k
       IF (i.eq.3) nz    = k
       IF (i.eq.4) nt    = k
       PRINT*,'prep_obs.exe: Dimension name is:',i,udim(i),name(1:20)
      ENDDO
      status             = NF_GET_ATT_REAL(ncid,nf_global,'DX',dx)
      status             = NF_GET_ATT_REAL(ncid,nf_global,'DY',dy)      
      PRINT*,'prep_obs.exe: nx =',nx
      PRINT*,'prep_obs.exe: ny =',ny
      PRINT*,'prep_obs.exe: nz =',nz
      PRINT*,'prep_obs.exe: nt =',nt
      PRINT*,'prep_obs.exe: dx =',dx
      PRINT*,'prep_obs.exe: dy =',dy            
!
! define a mother domain with A-grid based on the dims of
! PH from C-grid, and locate all variablies and read all the map
! information. Remember to convert geopotential to geopotential
! height for coordiante mapping at model level
!
      nx1                = nx + 1
      ny1                = ny + 1
      nz1                = nz + 1
      ALLOCATE(wrf_lat(nx,ny,nt),wrf_lon(nx,ny,nt))
      ALLOCATE(wrf_p(nx,ny,nz,nt),wrf_pp(nx,ny,nz,nt))
      ALLOCATE(wrf_z(nx,ny,nz,nt),wrf_zp(nx,ny,nz,nt))
      ALLOCATE(wrf_t(nx,ny,nz,nt),tem4d(nx,ny,nz1,nt))
! ... mass lat
      tem                = 'XLAT'
      status             = NF_INQ_VARID(ncid,tem(1:4),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,wrf_lat)
      if (debug.ge.1) print*,'prep_obs.exe: reading xlat returns:',status
! ... mass long
      tem                = 'XLONG'
      status             = NF_INQ_VARID(ncid,tem(1:5),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,wrf_lon)
      if (debug.ge.1) print*,'prep_obs.exe: reading xlong returns:',status
! ... base geopotential [m2 s^-2]
      tem                = 'PHB'
      status             = NF_INQ_VARID(ncid,tem(1:3),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,tem4d)
      wrf_z(:,:,1:nz,:)  = 0.5*(tem4d(:,:,1:nz,:) + tem4d(:,:,2:nz1,:)) 
      if (debug.ge.1) print*,'prep_obs.exe: reading wrf_z returns:',status
! ... geopotential perturbation
      tem                = 'PH'
      status             = NF_INQ_VARID(ncid,tem(1:2),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,tem4d)
      wrf_zp(:,:,1:nz,:) = 0.5*(tem4d(:,:,1:nz,:) + tem4d(:,:,2:nz1,:))
      if (debug.ge.1) print*,'prep_obs.exe: reading wrf_zp returns:',status
! ... base pressure
      tem                = 'PB'
      status             = NF_INQ_VARID(ncid,tem(1:2),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,wrf_p)
      if (debug.ge.1) print*,'prep_obs.exe: reading wrf_p returns:',status
! ... pressure pertubation
      tem                = 'P'
      status             = NF_INQ_VARID(ncid,tem(1:1),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,wrf_pp)
      if (debug.ge.1) print*,'prep_obs.exe: reading wrf_pp returns:',status
! ... potential temperature pertrubation w.r.t 300 K
      tem                = 'T'
      status             = NF_INQ_VARID(ncid,tem(1:2),pid)
      status             = NF_GET_VAR_REAL(ncid,pid,wrf_T)
      if (debug.ge.1) print*,'prep_obs.exe: reading wrf_T returns:',status
      if (debug.ge.1) then 
       print*,'wrf_lat(1,1,1)    = ',wrf_lat(1,1,1)
       print*,'wrf_lat(1,ny,1)   = ',wrf_lat(1,ny,1)
       print*,'wrf_lat(nx,1,1)   = ',wrf_lat(nx,1,1)
       print*,'wrf_lat(nx,ny,1)  = ',wrf_lat(nx,ny,1)
       print*,'wrf_lon(1,1,1)    = ',wrf_lon(1,1,1)
       print*,'wrf_lon(1,ny,1)   = ',wrf_lon(1,ny,1)
       print*,'wrf_lon(nx,1,1)   = ',wrf_lon(nx,1,1)
       print*,'wrf_lon(nx,ny,1)  = ',wrf_lon(nx,ny,1)
       write(*,'(1A5,5A16)')'k','pres(mb)','geop hgt(m)'
       do k              = 1,nz
         write(*,'(1I5,5F16.1)')k,wrf_p(10,10,k,1)/100,wrf_z(10,10,k,1)/G
       enddo   
      endif  
      wrf_z              = wrf_z/G
!
! reading bogus vortex data
!
      open(90,file='bvortex.txt',status='old')
      open(72,file='obs.dat')
      read(90,*)vertical_type,nox,noy,noz
      nmax                 = nox*noy*noz 
      print*,'prep_obs.exe: vertical_type = ',vertical_type
      print*,'prep_obs.exe: nox,noy,noz are:',nox,noy,noz
      allocate(u_o(nmax),v_o(nmax),t_o(nmax),z_o(nmax),zp_o(nmax))
      allocate(lat_o(nmax),lon_o(nmax),lev_o(nmax),q_o(nmax))
      read(90,*)
      i                  = 1
91    continue
      read(90,*,END=92)u_o(i),v_o(i),t_o(i),z_o(i),lat_o(i),lon_o(i),lev_o(i)
      i                  = i + 1
      goto 91
92    no                 = i - 1
      print*,'prep_obs.exe: num of obs data is: ',no
      if (debug.ge.1) then
       write(*,'(10F14.4)')u_o(1),v_o(1),t_o(1),z_o(1),lat_o(1),lon_o(1),lev_o(1)
       write(*,'(10F14.4)')u_o(no),v_o(no),t_o(no),z_o(no),lat_o(no),lon_o(no),lev_o(no)
      endif
      close(90)
      q_o                = mis
!
! pull the obs error information from the master namelist and put it
! along with the storm information
!
      CALL input_namelist(u_err,v_err,t_err,q_err,z_err)
      PRINT*,'prep_obs.exe: obs_error_u is: ',u_err
      PRINT*,'prep_obs.exe: obs_error_v is: ',v_err
      PRINT*,'prep_obs.exe: obs_error_t is: ',t_err
      PRINT*,'prep_obs.exe: obs_error_q is: ',q_err
      PRINT*,'prep_obs.exe: obs_error_p is: ',z_err
!
! do a mapping of the obs locations onto the grid dimension. For the time being,
! work for the Caresian coordinate only. OTher map projection requires more
! treatment
!
      if (vertical_type.eq.'z') then
       print*,'prep_obs.exe: vertical type is height'
!
! first count the number of actual obs within the domain
! 
       ncount            = 0
       do i              = 1,no
!
! ..... get the x/y position w.r.t to the wrf grid first
!
        j_o              = (lat_o(i)-wrf_lat(1,1,1))*111e3/dx
        i_o              = (lon_o(i)-wrf_lon(1,1,1))*111e3/dy
        ic               = int(i_o)
        jc               = int(j_o)
        if (i_o.gt.1.and.i_o.lt.nx.and.j_o.gt.1.and.j_o.lt.ny) then
!
! ..... search for the vertical levels
! 
         do k            = 1,nz-1
          if (wrf_z(ic,jc,k,1).le.lev_o(i).and.lev_o(i).lt.wrf_z(ic,jc,k+1,1)) then
            k_o          = k + (lev_o(i)-wrf_z(ic,jc,k,1))/(wrf_z(ic,jc,k+1,1)-wrf_z(ic,jc,k,1))
            ncount       = ncount + 1 
            goto 93
          endif
         enddo 
        endif
93      continue
       enddo 
       print*,'prep_obs.exe: number of used obs is: ',ncount
       write(72,'(1I9,3A9,10A12)')ncount,'lon','lat','lev','u','ue','v',                     &
                                  've','t','te','qv','qve','z','ze'
!
! now compute the location of obs and prinout to file along with errors
! information
!
       do i              = 1,no
        j_o              = (lat_o(i)-wrf_lat(1,1,1))*111e3/dx
        i_o              = (lon_o(i)-wrf_lon(1,1,1))*111e3/dy
        ic               = int(i_o)
        jc               = int(j_o)
        if (i_o.gt.1.and.i_o.lt.nx.and.j_o.gt.1.and.j_o.lt.ny) then
         do k            = 1,nz-1
          if (wrf_z(ic,jc,k,1).le.lev_o(i).and.lev_o(i).lt.wrf_z(ic,jc,k+1,1)) then
            w1           = (lev_o(i)-wrf_z(ic,jc,k,1))/(wrf_z(ic,jc,k+1,1)-wrf_z(ic,jc,k,1))
            w2           = 1. - w1 
            k_o          = k + w1
            t_o(i)       = t_o(i) + tref + wrf_T(ic,jc,k,1)*w2 + wrf_T(ic,jc,k+1,1)*w1
            zp_o(i)      = max(z_o(i)/G + wrf_zp(ic,jc,k,1)*w2 + wrf_zp(ic,jc,k+1,1)*w1,0.)
            write(72,'(3F12.4,10F12.4)')i_o,j_o,k_o,u_o(i),u_err,v_o(i),v_err,               &
                             t_o(i),t_err,q_o(i),q_err,zp_o(i),z_err
            goto 94
          endif
         enddo
        endif
94      continue
       enddo
      elseif (vertical_type.eq.'s') then
       print*,'prep_obs.exe: vertical type is sigma coordinate'
!
! first count the number of actual obs within the domain
!
       ncount            = 0
       do i              = 1,no
!
! ..... get the x/y position w.r.t to the wrf grid first
!
        j_o              = (lat_o(i)-wrf_lat(1,1,1))*111e3/dx
        i_o              = (lon_o(i)-wrf_lon(1,1,1))*111e3/dy
        ic               = int(i_o)
        jc               = int(j_o)
        if (i_o.gt.1.and.i_o.lt.nx.and.j_o.gt.1.and.j_o.lt.ny.and.lev_o(i).lt.nz.and.lev_o(i).gt.0) then
         ncount       = ncount + 1
        endif
       enddo
       print*,'prep_obs.exe: number of used obs is: ',ncount
       write(72,'(1I9,3A9,10A12)')ncount,'lon','lat','lev','u','ue','v',                     &
                                  've','t','te','qv','qve','z','ze'
!
! now compute the location of obs and prinout to file along with errors
! information
!
       do i              = 1,no
        j_o              = (lat_o(i)-wrf_lat(1,1,1))*111e3/dx
        i_o              = (lon_o(i)-wrf_lon(1,1,1))*111e3/dy
        ic               = int(i_o)
        jc               = int(j_o)
        if (i_o.gt.1.and.i_o.lt.nx.and.j_o.gt.1.and.j_o.lt.ny.and.lev_o(i).lt.nz.and.lev_o(i).gt.0) then
            k_o          = lev_o(i)
            t_o(i)       = t_o(i) + tref + wrf_T(ic,jc,nint(k_o),1)
            zp_o(i)      = z_o(i)/G + wrf_zp(ic,jc,nint(k_o),1)
            write(72,'(3F12.4,10F12.4)')i_o,j_o,k_o,u_o(i),u_err,v_o(i),v_err,               &
                             t_o(i),t_err,q_o(i),q_err,zp_o(i),z_err
        endif
       enddo
      elseif (vertical_type.eq.'p') then
       print*,'prep_obs.exe: vertical type is pressure'
      else
       print*,'prep_obs.exe: vertical type can only be z or p...exit'
       stop
      endif 
!
! close and quit all
!
      status  = NF_CLOSE(ncid)
      IF (status.ne.0) PRINT*,'prep_obs.exe: Can not close the new file' 
      END

      SUBROUTINE input_namelist(obs_err_u,obs_err_v,obs_err_t, &
                                obs_err_q,obs_err_p)
      INCLUDE "../Registry/letkf.inc"
      RETURN
      END
